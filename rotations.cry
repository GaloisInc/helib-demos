// Copyright (c) 2013-2014 Galois, Inc.
// Distributed under the terms of the GPLv3 license (see LICENSE file)
//
// Author: Brent Carmer
//
// Cryptol verification that the rotation algorithm in simon-blocks.cpp is
// correct.

rotateLeft400 : ([400], [6]) -> [400]
rotateLeft400 (x,n) = ((x << n) || (x >> (32-n))) && (zero # 0xFFFFFFFF)

rl400True : [32] -> [6] -> Bit
property rl400True z n = n >= 32 || (rotateLeft400 (zero # z, n) == zero # (z <<< n))



rotateLeft32 : ([32], [6]) -> [32]
rotateLeft32 (x,n) = (x << n) || (x >> (32 - n))

rl32True : [32] -> [6] -> Bit
property rl32True z n = n >= 32 || ((rotateLeft32 (z, n)) == z <<< n)



rotateLeft400Shai : ([400], [6]) -> [400]
rotateLeft400Shai (c,r) = tmp1 ^ tmp2
  where
    mask = zero # 0xFFFFFFFF
    tmp1 = (c << r)      && mask
    tmp2 = (c >> (32-r)) && mask

rl400ShaiTrue : [32] -> [6] -> Bit
property rl400ShaiTrue z n = n > 32 || rotateLeft400Shai (zero # z, n) == zero # (z <<< n)

